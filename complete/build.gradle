apply plugin: 'java'
apply plugin: 'eclipse'

apply plugin: 'application'
mainClassName = "messagingrabbitmq.Application"

repositories { mavenCentral() }

ext.springVersion = "3.2.2.RELEASE"

dependencies {
	compile "org.springframework:spring-context:$springVersion"
	compile 'org.springframework.amqp:spring-rabbit:1.1.4.RELEASE'
}

// Swap out default Spring Framework version with the latest release
configurations.all {
	resolutionStrategy.eachDependency { DependencyResolveDetails details ->
		if (details.requested.name.startsWith("spring") && 
			!details.requested.name.startsWith("spring-rabbit") &&
			!details.requested.name.startsWith("spring-amqp")) {
			details.useTarget group: details.requested.group, \
				name: details.requested.name, version: springVersion
		}
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.5'
}
